use crate::ast::expression::{Expr, BinaryOpcode, UnaryOpcode};
use crate::ast::value::Value;

use std::str::FromStr;

grammar;

pub Expr: Box<Expr> = {
    <left:Expr> ";" <right:Assignment> => Box::new(Expr::Concatenation{ left: left, right: right }),
    Assignment,
}

Term: Box<Expr> = {
    "let" <Identifier> => Box::new(Expr::Definition(<>)),
    "if" <exp:Expr> "{" <thn:Expr> "}" => Box::new(Expr::Condition{ exp: exp, then_block: thn, else_block: Box::new(Expr::Constant(Value::Int(0))) }),
    "if" <exp:Expr> "{" <thn:Expr> "}" "else" "{" <els:Expr> "}" => Box::new(Expr::Condition{ exp: exp, then_block: thn, else_block: els }),
    "{" <Expr> "}" => Box::new(Expr::Block(<>)),
    "(" <Expr> ")" => Box::new(Expr::Block(<>)),
    IntNum => Box::new(Expr::Constant(Value::Int(<>))),
    FloatNum => Box::new(Expr::Constant(Value::Float(<>))),
    StringLiteral => Box::new(Expr::Constant(Value::String(<>))),
    BoolLiteral => Box::new(Expr::Constant(Value::Bool(<>))),
    Identifier => Box::new(Expr::Identifier(<>)),
}

Assignment: Box<Expr> = {
    <Assignment> "=" <Disjunction> => Box::new(Expr::Assignment(<>)),
    Disjunction
}

Disjunction: Box<Expr> = {
    Disjunction DisjOp Conjunction => Box::new(Expr::BinaryOp(<>)),
    Conjunction,
}

DisjOp: BinaryOpcode = {
    "||" => BinaryOpcode::Disj,
}

Conjunction: Box<Expr> = {
    Conjunction ConjOp Comparison => Box::new(Expr::BinaryOp(<>)),
    Comparison,
}

ConjOp: BinaryOpcode = {
    "&&" => BinaryOpcode::Conj,
}

Comparison: Box<Expr> = {
    Comparison CompareOp Summ => Box::new(Expr::BinaryOp(<>)),
    Summ,
}

CompareOp: BinaryOpcode = {
    "==" => BinaryOpcode::Equals,
    "!=" => BinaryOpcode::NotEquals,
    "<" => BinaryOpcode::Lower,
    ">" => BinaryOpcode::Greater,
    "<=" => BinaryOpcode::LowerEquals,
    ">=" => BinaryOpcode::GreaterEquals,
}

Summ: Box<Expr> = {
    Summ SummOp Factor => Box::new(Expr::BinaryOp(<>)),
    Factor,
}

SummOp: BinaryOpcode = {
    "+" => BinaryOpcode::Add,
    "-" => BinaryOpcode::Sub,
}

Factor: Box<Expr> = {
    Factor FactorOp UnaryResult => Box::new(Expr::BinaryOp(<>)),
    UnaryResult,
}

FactorOp: BinaryOpcode = {
    "*" => BinaryOpcode::Mul,
    "/" => BinaryOpcode::Div,
}

UnaryResult: Box<Expr> = {
    UnaryOp Term => Box::new(Expr::UnaryOp(<>)),
    Term,
}

UnaryOp: UnaryOpcode = {
    "!" => UnaryOpcode::Not,
}

Identifier: String = {
    r"[a-zA-Z][a-zA-z_0-9]*" => <>.to_owned()
}

IntNum: i32 = {
    r"-?[0-9]+" => i32::from_str(<>).unwrap()
}

FloatNum: f32 = {
    r"-?[0-9]+\.[0-9]+" => f32::from_str(<>).unwrap()
}

StringLiteral: String = {
    r#""[^"]*""# => <>[1..<>.len() - 1].to_owned()
}

BoolLiteral: bool = {
    "true" => true,
    "false" => false,
}
