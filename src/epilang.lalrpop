use crate::compiler::ast::{AST, ClassAST, FunctionAST, FieldAST};
use crate::intermediate::opcode::{BinaryOpcode, UnaryOpcode};
use crate::intermediate::constant::Constant;

use std::str::FromStr;

grammar;

// Skip comments and whitespaces
match {
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { },
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },
    _
}

// Top level abstract syntax tree
pub AST: AST = {
    <ast:SemicolonTermination?> => match ast {
        None => AST::Constant(Constant::Unit), // Empty text is parsed as unit constant
        Some(exp) => exp
    }
}

SemicolonTermination: AST = {
    Concatenation,
    // Expressions ending with ';' are implicitly concatenated with unit
    <exp:Concatenation> ";" => {
        AST::Concatenation{ left: Box::new(exp), right: Box::new(AST::Constant(Constant::Unit)) }
    }
}

// Class definition
Class: ClassAST = {
    "class" <name:Identifier> "{"
        <fields:Sequence<FieldDecl>?>
        <methods:Sequence<FunctionDecl>?>
    "}" => {
        ClassAST {
            name: name,
            fields: fields.unwrap_or_default(),
            methods: methods.unwrap_or_default(),
        }
    }
}

Condition: AST = {
    // If without else
    "if" <exp:Concatenation> "{" <thn:AST> "}" => {
        AST::Condition {
            exp: Box::new(exp),
            then_block: Box::new(thn),
            else_block: Box::new(AST::Constant(Constant::Unit))
        }
    },
    // If with else
    "if" <exp:Concatenation> "{" <thn:AST> "}" "else" "{" <els:AST> "}" => {
        AST::Condition {
            exp: Box::new(exp),
            then_block: Box::new(thn),
            else_block: Box::new(els)
        }
    },
    // Else if
    "if" <exp:Concatenation> "{" <thn:AST> "}" "else" <else_if:Condition> => {
        AST::Condition {
            exp: Box::new(exp),
            then_block: Box::new(thn),
            else_block: Box::new(else_if)
        }
    },
}

Loop: AST = {
    // While loop
    "while" <guard:Concatenation> "{" <exp:AST> "}" => {
        AST::While { guard: Box::new(guard), exp: Box::new(exp) }
    },
}

// Field declaration
FieldDecl: FieldAST = {
    "let" <Identifier> => FieldAST { name: <> }
}

// Function declaration
FunctionDecl: FunctionAST = {
    "fn" <name:Identifier> "(" <args:Comma<Identifier>?> ")" "{" <body:AST> "}" => {
        FunctionAST { name: name, args: args.unwrap_or_default(), body: body }
    },
}

// Anonymous function aka closure
AnonFunction: AST = {
    "fn" "(" <args:Comma<Identifier>?> ")" "{" <exp:AST> "}" => {
        AST::Closure { args: args.unwrap_or_default(), exp: Box::new(exp) }
    },
}

Term: AST = {
    "[" <elems:Comma<Disjunction>?> "]" => AST::List { elements: elems.unwrap_or_default() }, // List creation
    "let" <Identifier> => AST::Definition(<>), // Variable declaration
    "{" <AST> "}" => AST::Block(Box::new(<>)), // Blocks
    "(" ")" => AST::Constant(Constant::Unit), // Empty round brackets are parsed as unit
    "(" <SemicolonTermination> ")", // Round brackets
    Class => AST::Class(Box::new(<>)),
    FunctionDecl => AST::Function(Box::new(<>)),
    IntNum => AST::Constant(Constant::Int(<>)),
    FloatNum => AST::Constant(Constant::Float(<>)),
    StringLiteral => AST::Constant(Constant::String(<>)),
    BoolLiteral => AST::Constant(Constant::Bool(<>)),
    Identifier => AST::Identifier(<>),
    Condition,
    Loop,
    AnonFunction,
}

// <exp1> ; <exp2>
Concatenation: AST = {
    Assignment,
    <left:Concatenation> ";" <right:Assignment> => {
        AST::Concatenation{ left: Box::new(left), right: Box::new(right) }
    },
}

// <exp1> = <exp2>
Assignment: AST = {
    Disjunction,
    <left:Assignment> "=" <right:Disjunction> => {
        AST::Assignment(Box::new(left), Box::new(right))
    },
}

// <exp1> || <exp2>
Disjunction: AST = {
    Conjunction,
    <exp1:Disjunction> <op:DisjOp> <exp2:Conjunction> => {
        AST::BinaryOp(Box::new(exp1), op, Box::new(exp2))
    },
}

DisjOp: BinaryOpcode = {
    "||" => BinaryOpcode::Or,
}

// <exp1> && <exp2>
Conjunction: AST = {
    Comparison,
    <exp1:Conjunction> <op:ConjOp> <exp2:Comparison> => {
        AST::BinaryOp(Box::new(exp1), op, Box::new(exp2))
    },
}

ConjOp: BinaryOpcode = {
    "&&" => BinaryOpcode::And,
}

// Arithmetical comparisons
Comparison: AST = {
    Summ,
    <exp1:Comparison> <op:CompareOp> <exp2:Summ> => {
        AST::BinaryOp(Box::new(exp1), op, Box::new(exp2))
    },
}

CompareOp: BinaryOpcode = {
    "==" => BinaryOpcode::Equals,
    "!=" => BinaryOpcode::NotEquals,
    "<" => BinaryOpcode::Lower,
    ">" => BinaryOpcode::Greater,
    "<=" => BinaryOpcode::LowerEquals,
    ">=" => BinaryOpcode::GreaterEquals,
}

// Summ or subtraction
Summ: AST = {
    Factor,
    <exp1:Summ> <op:SummOp> <exp2:Factor> => {
        AST::BinaryOp(Box::new(exp1), op, Box::new(exp2))
    },
}

SummOp: BinaryOpcode = {
    "+" => BinaryOpcode::Add,
    "-" => BinaryOpcode::Sub,
}

// Multiplication or division
Factor: AST = {
    UnaryResult,
    <exp1:Factor> <op:FactorOp> <exp2:UnaryResult> => {
        AST::BinaryOp(Box::new(exp1), op, Box::new(exp2))
    },
}

FactorOp: BinaryOpcode = {
    "*" => BinaryOpcode::Mul,
    "/" => BinaryOpcode::Div,
}

// Unary operators
UnaryResult: AST = {
    CallOrSubscript,
    <op:UnaryOp> <exp:CallOrSubscript> => AST::UnaryOp(op, Box::new(exp)),
}

UnaryOp: UnaryOpcode = {
    "!" => UnaryOpcode::Not,
}

CallOrSubscript: AST = {
    PropertyAccess,
    // Function call
    <fun:CallOrSubscript> "(" <args:Comma<Disjunction>?> ")" => {
        AST::FunctionCall { fun: Box::new(fun), args: args.unwrap_or_default()}
    },
    // List subscript
    <list:CallOrSubscript> "[" <index:Disjunction> "]" => {
        AST::Subscript { element: Box::new(list), index: Box::new(index)}
    },
}

// Field or method access
PropertyAccess: AST = {
    Term,
    <exp: PropertyAccess> "." <name:Identifier> => {
        AST::PropertyAccess { exp: Box::new(exp), property: name }
    },
}

// String identifier to name variables, fields, classes etc
Identifier: String = {
    r"[a-zA-Z][a-zA-Z_0-9]*" => <>.to_owned()
}

// Integer number
IntNum: i32 = {
    r"-?[0-9]+" => i32::from_str(<>).unwrap()
}

// Float number
FloatNum: f32 = {
    r"-?[0-9]+\.[0-9]+" => f32::from_str(<>).unwrap()
}

// String literal (eg: "Hello World")
StringLiteral: String = {
    r#""[^"]*""# => <>[1..<>.len() - 1].to_owned()
}

// Boolean
BoolLiteral: bool = {
    "true" => true,
    "false" => false,
}

// Sequence of elements with no separator
Sequence<T>: Vec<T> = {
    <e:T> => vec![e],
    <mut list:Sequence<T>> <e:T> => {
        list.push(e);
        list
    }
}

// Sequence of elements separated by ','
Comma<T>: Vec<T> = {
    <e:T> => vec![e],
    <mut list:Comma<T>> "," <e:T> => {
        list.push(e);
        list
    }
}
